# Copyright (c) 2013 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/util/lastchange.gni")

action("chromium_git_revision") {
  script = "lastchange.py"

  output_file = "$target_gen_dir/chromium_git_revision.h"
  outputs = [ output_file ]

  args = [
    "--header",
    rebase_path(output_file, root_build_dir),
    "--revision-id-only",
    "--revision-id-prefix",
    "@",
    "-m CHROMIUM_GIT_REVISION",
  ]
}

action("chrome_version_json") {
  script = "version.py"
  _chrome_version_path = "//chrome/VERSION"
  inputs = [ _chrome_version_path ]
  _output_file = "$root_gen_dir/CHROME_VERSION.json"
  outputs = [ _output_file ]
  args = [
    "--file",
    rebase_path(_chrome_version_path, root_build_dir),
    "--template",
    "{\"full-quoted\": \"\\\"@MAJOR@.@MINOR@.@BUILD@.@PATCH@\\\"\"}",
    "--output",
    rebase_path(_output_file, root_build_dir),
  ]
}
