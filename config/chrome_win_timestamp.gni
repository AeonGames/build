# Copyright 2023 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
#
# Defines the chrome_win_build_timestamp variable.
# This is a Chrome-Windows specific version of timestamp.gni, needed so that the
# chrome/VERSION file can be passed in to the script.

import("//build/util/lastchange.gni")

if (is_official_build) {
  official_name = "official"
} else {
  official_name = "default"
}

# This will return a timestamp that's different each day (official builds)
# or each month (regular builds).  Just rely on gn rerunning due to other
# changes to keep this up to date.  (Bots run gn on each build, and for devs
# the timestamp being 100% accurate doesn't matter.)
# See compute_build_timestamp.py for tradeoffs for picking the timestamp.
chrome_win_build_timestamp =
    exec_script("../compute_build_timestamp.py",
                [
                  official_name,
                  "--version_file=../../chrome/VERSION",
                ],
                "trim string",
                [ lastchange_file ])
